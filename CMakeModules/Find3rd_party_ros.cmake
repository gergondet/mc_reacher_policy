if(NOT TARGET test_controller::ROS)
  include(FindPkgConfig)
  pkg_check_modules(TEST_CONTROLLER_roscpp QUIET roscpp)
  if(${TEST_CONTROLLER_roscpp_FOUND})
    set(ROSCPP_FOUND True)
    set(TEST_CONTROLLER_ROS_DEPENDENCIES ${3rd_party_ros_FIND_COMPONENTS})
    foreach(DEP ${TEST_CONTROLLER_ROS_DEPENDENCIES})
      message(STATUS "Looking for ROS component ${DEP}")
      pkg_check_modules(TEST_CONTROLLER_${DEP} REQUIRED ${DEP})
      list(APPEND TEST_CONTROLLER_ROS_LIBRARIES ${TEST_CONTROLLER_${DEP}_LIBRARIES})
      list(APPEND TEST_CONTROLLER_ROS_LIBRARY_DIRS ${TEST_CONTROLLER_${DEP}_LIBRARY_DIRS})
      list(APPEND TEST_CONTROLLER_ROS_INCLUDE_DIRS ${TEST_CONTROLLER_${DEP}_INCLUDE_DIRS})
      foreach(FLAG ${TEST_CONTROLLER_${DEP}_LDFLAGS})
        if(IS_ABSOLUTE ${FLAG})
          list(APPEND TEST_CONTROLLER_ROS_FULL_LIBRARIES ${FLAG})
        endif()
      endforeach()
    endforeach()
    list(REMOVE_DUPLICATES TEST_CONTROLLER_ROS_LIBRARIES)
    list(REMOVE_DUPLICATES TEST_CONTROLLER_ROS_LIBRARY_DIRS)
    list(REMOVE_DUPLICATES TEST_CONTROLLER_ROS_INCLUDE_DIRS)
    foreach(LIB ${TEST_CONTROLLER_ROS_LIBRARIES})
      string(SUBSTRING "${LIB}" 0 1 LIB_STARTS_WITH_COLUMN)
      if(${LIB_STARTS_WITH_COLUMN} STREQUAL ":")
        string(SUBSTRING "${LIB}" 1 -1 LIB)
      endif()
      if(IS_ABSOLUTE ${LIB})
        list(APPEND TEST_CONTROLLER_ROS_FULL_LIBRARIES ${LIB})
      else()
        find_library(${LIB}_FULL_PATH NAME ${LIB} HINTS ${TEST_CONTROLLER_ROS_LIBRARY_DIRS})
        list(APPEND TEST_CONTROLLER_ROS_FULL_LIBRARIES ${${LIB}_FULL_PATH})
      endif()
    endforeach()
    list(REMOVE_DUPLICATES TEST_CONTROLLER_ROS_FULL_LIBRARIES)
    add_library(test_controller::ROS INTERFACE IMPORTED)
    set_target_properties(test_controller::ROS PROPERTIES
      INTERFACE_LINK_LIBRARIES "${TEST_CONTROLLER_ROS_FULL_LIBRARIES}"
      INTERFACE_INCLUDE_DIRECTORIES "${TEST_CONTROLLER_ROS_INCLUDE_DIRS}"
    )
    message("-- Found ROS libraries: ${TEST_CONTROLLER_ROS_FULL_LIBRARIES}")
    message("-- Found ROS include directories: ${TEST_CONTROLLER_ROS_INCLUDE_DIRS}")
  else()
    set(ROSCPP_FOUND False)
  endif()
endif()
