if(NOT TARGET mc_reacher_policy::ROS)
  include(FindPkgConfig)
  pkg_check_modules(MC_REACHER_POLICY_roscpp QUIET roscpp)
  if(${MC_REACHER_POLICY_roscpp_FOUND})
    set(ROSCPP_FOUND True)
    set(MC_REACHER_POLICY_ROS_DEPENDENCIES ${3rd_party_ros_FIND_COMPONENTS})
    foreach(DEP ${MC_REACHER_POLICY_ROS_DEPENDENCIES})
      message(STATUS "Looking for ROS component ${DEP}")
      pkg_check_modules(MC_REACHER_POLICY_${DEP} REQUIRED ${DEP})
      list(APPEND MC_REACHER_POLICY_ROS_LIBRARIES ${MC_REACHER_POLICY_${DEP}_LIBRARIES})
      list(APPEND MC_REACHER_POLICY_ROS_LIBRARY_DIRS ${MC_REACHER_POLICY_${DEP}_LIBRARY_DIRS})
      list(APPEND MC_REACHER_POLICY_ROS_INCLUDE_DIRS ${MC_REACHER_POLICY_${DEP}_INCLUDE_DIRS})
      foreach(FLAG ${MC_REACHER_POLICY_${DEP}_LDFLAGS})
        if(IS_ABSOLUTE ${FLAG})
          list(APPEND MC_REACHER_POLICY_ROS_FULL_LIBRARIES ${FLAG})
        endif()
      endforeach()
    endforeach()
    list(REMOVE_DUPLICATES MC_REACHER_POLICY_ROS_LIBRARIES)
    list(REMOVE_DUPLICATES MC_REACHER_POLICY_ROS_LIBRARY_DIRS)
    list(REMOVE_DUPLICATES MC_REACHER_POLICY_ROS_INCLUDE_DIRS)
    foreach(LIB ${MC_REACHER_POLICY_ROS_LIBRARIES})
      string(SUBSTRING "${LIB}" 0 1 LIB_STARTS_WITH_COLUMN)
      if(${LIB_STARTS_WITH_COLUMN} STREQUAL ":")
        string(SUBSTRING "${LIB}" 1 -1 LIB)
      endif()
      if(IS_ABSOLUTE ${LIB})
        list(APPEND MC_REACHER_POLICY_ROS_FULL_LIBRARIES ${LIB})
      else()
        find_library(${LIB}_FULL_PATH NAME ${LIB} HINTS ${MC_REACHER_POLICY_ROS_LIBRARY_DIRS})
        list(APPEND MC_REACHER_POLICY_ROS_FULL_LIBRARIES ${${LIB}_FULL_PATH})
      endif()
    endforeach()
    list(REMOVE_DUPLICATES MC_REACHER_POLICY_ROS_FULL_LIBRARIES)
    add_library(mc_reacher_policy::ROS INTERFACE IMPORTED)
    set_target_properties(mc_reacher_policy::ROS PROPERTIES
      INTERFACE_LINK_LIBRARIES "${MC_REACHER_POLICY_ROS_FULL_LIBRARIES}"
      INTERFACE_INCLUDE_DIRECTORIES "${MC_REACHER_POLICY_ROS_INCLUDE_DIRS}"
    )
    message("-- Found ROS libraries: ${MC_REACHER_POLICY_ROS_FULL_LIBRARIES}")
    message("-- Found ROS include directories: ${MC_REACHER_POLICY_ROS_INCLUDE_DIRS}")
  else()
    set(ROSCPP_FOUND False)
  endif()
endif()
